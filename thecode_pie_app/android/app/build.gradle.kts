import java.util.Properties
import java.io.File

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
    id("com.google.gms.google-services")
}


val localProperties = Properties()
val localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.inputStream().use { localProperties.load(it) }
}

android {
    namespace = "com.clavis.thecodepie"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.clavis.thecodepie"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        manifestPlaceholders += mapOf(
            "admobAppId" to (localProperties.getProperty("admob.app.id") ?: "")
        )
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig = signingConfigs.getByName("debug")
        }
    }
}

// AdMob (local.properties):
//   admob.app.id          = 앱 ID (ca-app-pub-xxx~yyy) → AndroidManifest
//   admob.reward.unit.id  = 리워드 광고 단위 ID (ca-app-pub-xxx/zzz) → ad_config.g.dart
tasks.register("generateAdConfig") {
    doLast {
        val rewardUnitId = localProperties.getProperty("admob.reward.unit.id") ?: ""
        val flutterRoot = rootProject.projectDir.parentFile!!
        val adConfigDir = File(flutterRoot, "lib/core/config")
        adConfigDir.mkdirs()
        val content = """// Generated by Android build from local.properties - do not edit.
const String admobAndroidRewardUnitId = r'$rewardUnitId';
"""
        File(adConfigDir, "ad_config.g.dart").writeText(content)
    }
}
afterEvaluate {
    listOf("compileFlutterBuildDebug", "compileFlutterBuildProfile", "compileFlutterBuildRelease")
        .forEach { name ->
            tasks.findByName(name)?.dependsOn("generateAdConfig")
        }
}

flutter {
    source = "../.."
}
